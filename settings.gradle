import kotlin.KotlinVersion

pluginManagement {
    plugins {
        id 'org.jetbrains.kotlin.jvm' version "${KotlinVersion.CURRENT}"
    }
    repositories {
        gradlePluginPortal()
        mavenCentral()
    }
}

plugins {
    id "io.freefair.settings.plugin-versions" version "8.12.1"
    id "com.gradle.develocity" version "3.19.2"
}

enableFeaturePreview "STABLE_CONFIGURATION_CACHE"

boolean isCiServer = System.getenv().containsKey("CI")

develocity {
    buildScan {
        publishing.onlyIf { isCiServer }
        termsOfUseUrl = "https://gradle.com/help/legal-terms-of-use"
        termsOfUseAgree = "yes"
    }
}

buildCache {
    remote(HttpBuildCache) {
        url = 'https://build-cache.grefer-hosting.de/cache/'
        push = isCiServer
        credentials {
            username = "freefair"
            password = providers.gradleProperty('freefairBuildCachePassword').getOrElse(null)
        }
    }
}

rootProject.name = 'gradle-plugins'

include "test-common"
include "documentation"

include "plugin-utils"

include "maven-plugin"
include "maven-plugin-plugin"
include "lombok-plugin"
include "embedded-sass-plugin"
include "aspectj-plugin"
include "compress-plugin"
include "code-generator-plugin"
include "code-generator-api"
include "test-code-generator"
include "jacoco-plugin"
include "github-plugin"
include "okhttp-plugin"
include "git-plugin"
include "mkdocs-plugin"
include "quicktype-plugin"
include "plantuml-plugin"

include "mjml-plugin"

include "settings-plugin"

include "gradle-plugin-platform"
