apply plugin: "maven-publish"
apply plugin: "java-gradle-plugin"
apply plugin: "com.gradle.plugin-publish"

dependencies {
    implementation 'io.github.classgraph:classgraph:4.8.87'
    api project(':code-generator-api')
    testImplementation project(':test-common')

}

task copyTestGenerator(type: Copy, dependsOn: ':test-code-generator:jar'){
    from  tasks.getByPath(':test-code-generator:jar').outputs
    into 'src/test/resources/'
    eachFile {
        name = "test-code-generator.jar"
    }
}

test.dependsOn(copyTestGenerator)

gradlePlugin {
    plugins {
        codeGenerator {
            id = "io.freefair.code-generator"
            implementationClass = "io.freefair.gradle.plugin.codegenerator.CodeGeneratorPlugin"
        }
    }
}

pluginBundle {
    plugins {
        codeGenerator {
            displayName = "Code Generator Plugin"
            description = "A plugin for executing specified code generators"
            tags = ['code', 'generation', 'codegeneration', 'generate']
        }
    }
}
