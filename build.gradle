buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.21.0"

        def pluginVersion = "6.4.2"
        classpath "io.freefair.gradle:maven-plugin:$pluginVersion"
        classpath "io.freefair.gradle:lombok-plugin:$pluginVersion"
        classpath "io.freefair.gradle:git-plugin:$pluginVersion"
    }
}

plugins {
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
}

apply plugin: 'io.freefair.git-version'

nexusPublishing {
    repositories {
        sonatype {
            stagingProfileId = "7e6204597a774f"
        }
    }
}

allprojects {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }

    group = "io.freefair.gradle"
    version = rootProject.version

    plugins.withId("java") {

        java {
            withSourcesJar()
            withJavadocJar()
            toolchain {
                languageVersion = JavaLanguageVersion.of(8)
            }
        }

        plugins.apply "jacoco"
        plugins.apply "io.freefair.lombok"
        plugins.apply "io.freefair.javadoc-links"

        jacocoTestReport.reports.xml.required = true

        test.useJUnitPlatform()

        dependencies {
            testImplementation 'org.assertj:assertj-core:3.22.0'
            testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
            testImplementation 'org.junit.jupiter:junit-jupiter-params:5.8.2'
            testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
        }
    }

    tasks.withType(Javadoc).configureEach {
        options.links "https://docs.gradle.org/$gradle.gradleVersion/javadoc/"
        options.addBooleanOption("Xdoclint:-missing", true)
        failOnError = false
    }

    plugins.withId("com.gradle.plugin-publish") {
        pluginBundle {
            website = "https://docs.freefair.io/gradle-plugins/$version/reference/"
            vcsUrl = 'https://github.com/freefair/gradle-plugins'
        }
    }

    plugins.withId("maven-publish") {
        publishing {
            publications.withType(MavenPublication) {
                pom {
                    url = 'https://github.com/freefair/gradle-plugins'
                    name = provider { project.description }
                    description = provider { project.description }
                    inceptionYear = '2016'
                    licenses {
                        license {
                            name = 'MIT'
                            url = 'https://github.com/freefair/gradle-plugins/blob/master/LICENSE'
                        }
                    }
                    organization {
                        name = 'FreeFair'
                        url = 'https://github.com/freefair'
                    }
                    developers {
                        developer {
                            id = 'larsgrefer'
                            name = 'Lars Grefer'
                            email = 'github@larsgrefer.de'
                            timezone = 'Europe/Berlin'
                        }
                        developer {
                            id = 'frisch12'
                            name = 'Dennis Fricke'
                            email = 'dennis.fricke@freefair.io'
                            timezone = 'Europe/Berlin'
                        }
                    }
                    ciManagement {
                        system = 'GitHub Actions'
                        url = 'https://github.com/freefair/gradle-plugins/actions'
                    }
                    issueManagement {
                        system = 'GitHub Issues'
                        url = 'https://github.com/freefair/gradle-plugins/issues'
                    }
                    scm {
                        connection = 'scm:git:https://github.com/freefair/gradle-plugins.git'
                        developerConnection = 'scm:git:git@github.com:freefair/gradle-plugins.git'
                        url = 'https://github.com/freefair/gradle-plugins/'
                    }
                }
            }
        }
    }
}

asciidoctor {
    baseDirFollowsSourceDir()

    attributes gradle_version: gradle.gradleVersion

    inputs.dir("src/docs/asciidoc")
}

apply plugin: "io.freefair.aggregate-javadoc"
apply plugin: "io.freefair.javadoc-links"

tasks.register("docsZip", Zip) {
    group = 'build'
    archiveClassifier = 'docs'
    dependsOn asciidoctor

    into("api") {
        from aggregateJavadoc
    }
    into("reference") {
        from asciidoctor.outputDir
    }
}

tasks.register("uploadDocs", io.freefair.gradle.plugins.okhttp.tasks.UploadFile) {
    dependsOn docsZip
    username = findProperty("freefairDocsUser")
    password = findProperty("freefairDocsPass")
    file = docsZip.archiveFile
    contentType = 'application/zip'
    url = "https://docs.freefair.io/api/$project.version?path=gradle-plugins"
}

tasks.named('wrapper') {
    distributionType = Wrapper.DistributionType.ALL
}
